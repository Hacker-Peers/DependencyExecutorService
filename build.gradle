plugins {
    id 'com.github.kt3k.coveralls' version '2.8.1'
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

group 'org.hackerpeers'
archivesBaseName = name
version '1.0-SNAPSHOT'

sourceCompatibility = 1.7
targetCompatibility = 1.7


test {
    // enable TestNG support (default is JUnit)
    useTestNG()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

artifacts {
    archives javadocJar, sourcesJar
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testCompile group: 'org.testng', name: 'testng', version: '6.10'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Dependency Executor Service'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Library to facilitate the management of tasks having dependencies.'
                url 'https://github.com/Hacker-Peers/DependencyExecutorService'

                scm {
                    connection 'scm:git:https://github.com/Hacker-Peers/DependencyExecutorService.git'
                    developerConnection 'https://github.com/Hacker-Peers/DependencyExecutorService.git'
                    url 'https://github.com/Hacker-Peers/DependencyExecutorService.git'
                    branch 'master'
                    tag 'HEAD'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'sberthiaume'
                        name 'Simon Berthiaume'
                        email 'sberthiaume@hackerpeers.org'
                    }
                }
            }
        }
    }
}
